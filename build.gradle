buildscript {
	ext {
		springBootVersion = '2.1.1.RELEASE'
		dbDriver = 'org.h2.Driver'
		dbUsername = 'sa'
		dbPassword = ''
		dbUrl = 'jdbc:h2:file:./build/orders'
		runList = 'schema,data'
	}
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
		classpath "org.liquibase:liquibase-gradle-plugin:2.0.1"
	}
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'org.liquibase.gradle'

group = 'it.vb.sample'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8

repositories {
	mavenCentral()
}

liquibase {
  activities {
    schema {
      changeLogFile 'src/main/resources/db/changelog/db.changelog-master.yml'
      url project.ext.dbUrl
	  username project.ext.dbUsername
	  password project.ext.dbPassword
	  driver project.ext.dbDriver
      referenceUrl 'hibernate:spring:it.vb.sample.demo.entities?dialect=org.hibernate.dialect.H2Dialect'
    }
	data {
	  changeLogFile 'src/main/resources/db/changelog/db.changelog-data.yml'
      url project.ext.dbUrl
	  username project.ext.dbUsername
	  password project.ext.dbPassword
	  driver project.ext.dbDriver
	  diffTypes 'data'
	}
  }

  runList = project.ext.runList
}

diff.dependsOn compileJava
diffChangeLog.dependsOn compileJava
generateChangelog.dependsOn compileJava

dependencies {
	implementation('org.springframework.boot:spring-boot-starter-data-jpa')
	implementation('org.springframework.boot:spring-boot-starter-data-rest')
	implementation('org.springframework.boot:spring-boot-starter-web')
	testImplementation('org.springframework.boot:spring-boot-starter-test')
	testImplementation('org.springframework.restdocs:spring-restdocs-mockmvc')

	// https://mvnrepository.com/artifact/com.h2database/h2
	compile('com.h2database:h2:1.4.197')

	// https://mvnrepository.com/artifact/org.liquibase/liquibase-core
    compile group: 'org.liquibase', name: 'liquibase-core', version: '3.6.2'

	// https://mvnrepository.com/artifact/io.springfox/springfox-swagger2
    compile group: 'io.springfox', name: 'springfox-swagger2', version: '2.9.2'

	// https://mvnrepository.com/artifact/io.springfox/springfox-swagger-ui
    compile group: 'io.springfox', name: 'springfox-swagger-ui', version: '2.9.2'

	compile group: 'io.springfox', name: 'springfox-data-rest', version: '2.9.2'

	liquibaseRuntime('org.liquibase:liquibase-core:3.6.2')
	//liquibaseRuntime('org.liquibase:liquibase-groovy-dsl:2.0.1')
	liquibaseRuntime('org.liquibase.ext:liquibase-hibernate5:3.6')
	liquibaseRuntime('com.h2database:h2:1.4.197')
	liquibaseRuntime('org.springframework.boot:spring-boot-starter-data-jpa')
	liquibaseRuntime sourceSets.main.output

}
