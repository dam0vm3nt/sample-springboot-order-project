buildscript {
	ext {
		springBootVersion = '2.1.1.RELEASE'
		dbDriver = 'org.hsqldb.jdbc.JDBCDriver'
		dbUrl = 'jdbc:hsqldb:file:./build/demo.db'
	}
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
		classpath "org.liquibase:liquibase-gradle-plugin:2.0.1"
	}
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'org.liquibase.gradle'

group = 'it.vb.sample'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8

repositories {
	mavenCentral()
}

liquibase {
  activities {
    diffMain {
      changeLogFile 'src/main/resources/db/changelog/db.changelog-master.yml'
      url project.ext.dbUrl
	  driver project.ext.dbDriver
      referenceUrl 'hibernate:spring:it.vb.sample.demo.entities?dialect=org.hibernate.dialect.HSQLDialect'
    }
	diffData {
	  changeLogFile 'src/main/resources/db/changelog/db.changelog-data.yml'
      url project.ext.dbUrl
	  driver project.ext.dbDriver
	  diffTypes 'data'
	  referenceUrl project.ext.dbUrl
	}
  }
}

diff.dependsOn compileJava
diffChangeLog.dependsOn compileJava
generateChangelog.dependsOn compileJava

dependencies {
	implementation('org.springframework.boot:spring-boot-starter-data-jpa')
	implementation('org.springframework.boot:spring-boot-starter-data-rest')
	implementation('org.springframework.boot:spring-boot-starter-web')
	testImplementation('org.springframework.boot:spring-boot-starter-test')
	testImplementation('org.springframework.restdocs:spring-restdocs-mockmvc')

	// https://mvnrepository.com/artifact/org.hsqldb/hsqldb
    compile group: 'org.hsqldb', name: 'hsqldb', version: '2.4.1'

	// https://mvnrepository.com/artifact/org.liquibase/liquibase-core
    compile group: 'org.liquibase', name: 'liquibase-core', version: '3.6.2'

	liquibaseRuntime('org.liquibase:liquibase-core:3.6.2')
	//liquibaseRuntime('org.liquibase:liquibase-groovy-dsl:2.0.1')
	liquibaseRuntime('org.liquibase.ext:liquibase-hibernate5:3.6')
	liquibaseRuntime('org.hsqldb:hsqldb:2.4.1')
	liquibaseRuntime('org.springframework.boot:spring-boot-starter-data-jpa')
	liquibaseRuntime sourceSets.main.output

}
